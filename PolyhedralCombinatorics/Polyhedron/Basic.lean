import PolyhedralCombinatorics.Polyhedron.Defs

namespace Polyhedron
open Matrix LinearSystem

variable {ğ”½} [LinearOrderedField ğ”½] {n : â„•} (p : Polyhedron ğ”½ n)

@[simp] theorem toSet_ofLinearSystem {d : LinearSystem ğ”½ n} : (ofLinearSystem d).toSet = d.toSet := rfl

@[simp] theorem empty_toSet : (âˆ… : Polyhedron ğ”½ n).toSet = âˆ… := by
  change empty.toSet = âˆ…
  rw [empty, toSet_ofLinearSystem, Set.eq_empty_iff_forall_not_mem]
  intro x h
  have := calc
    1 â‰¤ -((fun _ â†¦ 1) â¬áµ¥ x) := le_neg.mpr $ h 0
    _ = (fun _ â†¦ -1) â¬áµ¥ x := by simp [dotProduct]
    _ â‰¤ 0 := h 1
  linarith

theorem mem_def : x âˆˆ p â†” x âˆˆ p.toSet := Iff.rfl

/-- The empty polyhedron has no points. -/
theorem not_mem_empty : âˆ€ x, x âˆ‰ (âˆ… : Polyhedron ğ”½ n) := by simp [mem_def]

@[simp] theorem univ_toSet : (âŠ¤ : Polyhedron ğ”½ n).toSet = Set.univ := by
  show univ.toSet = Set.univ
  simp [univ]

/-- The empty polyhedron contains all points. -/
theorem mem_univ : âˆ€ x, x âˆˆ (âŠ¤ : Polyhedron ğ”½ n) := by simp [mem_def]

theorem toSet_inj {p q : Polyhedron ğ”½ n} : p.toSet = q.toSet â†” p = q := by
  constructor <;> intro h
  . induction p, q using Quotient.indâ‚‚
    rename_i p q
    rw [Quotient.eq]
    show p.toSet = q.toSet
    simp_all only [toSet, Quotient.lift_mk]
  . subst h
    rfl

@[simp] theorem ofLinearSystem_eq_ofLinearSystem {d d'  : LinearSystem ğ”½ n}
  : ofLinearSystem d = ofLinearSystem d' â†” d.toSet = d'.toSet := by
  simp_rw [â†toSet_inj, ofLinearSystem, toSet, Quotient.lift_mk]

/-- The set of points of a polyhedron is convex. -/
theorem toSet_convex : Convex ğ”½ p.toSet :=
  Quot.recOn p
    (fun p x x_mem_P y y_mem_P Î± Î² Î±_nonneg Î²_nonneg Î±Î²_affine â†¦ by
      calc
        p.mat *áµ¥ (Î± â€¢ x + Î² â€¢ y) = Î± â€¢ p.mat *áµ¥ x + Î² â€¢ p.mat *áµ¥ y := by
          simp_rw [mulVec_add, mulVec_smul]
        _ â‰¤ Î± â€¢ p.vec + Î² â€¢ p.vec :=
          add_le_add
            (smul_le_smul_of_nonneg_left x_mem_P Î±_nonneg)
            (smul_le_smul_of_nonneg_left y_mem_P Î²_nonneg)
        _ = p.vec := by rw [â†add_smul, Î±Î²_affine, one_smul])
    (fun _ _ _ â†¦ rfl)

@[simp] theorem mem_ofConstraints {constraints : List (LinearConstraint ğ”½ n)}
  : âˆ€ x, x âˆˆ ofLinearSystem (ofConstraints constraints) â†” âˆ€ c âˆˆ constraints, c.valid x :=
  LinearSystem.mem_toSet_ofConstraints

/-- The set of points of a polyhedron defined by a given list of `constraints` is the set of
  points that satisfy those constraints. -/
@[simp] theorem toSet_ofConstraints {constraints : List (LinearConstraint ğ”½ n)}
  : (ofConstraints constraints).toSet = { x | âˆ€ constr âˆˆ constraints, constr.valid x } :=
  LinearSystem.toSet_ofConstraints

/-- Intersection of polyhedra. -/
def inter : Polyhedron ğ”½ n â†’ Polyhedron ğ”½ n â†’ Polyhedron ğ”½ n :=
  Quotient.liftâ‚‚ (ofLinearSystem $ concat Â· Â·) $ by
    intro a b a' b' ha hb
    simp_rw [ofLinearSystem_eq_ofLinearSystem, toSet_concat]
    change a.toSet = a'.toSet at ha
    change b.toSet = b'.toSet at hb
    simp_all only

instance : Inter (Polyhedron ğ”½ n) := âŸ¨interâŸ©

@[simp] theorem toSet_inter (p q : Polyhedron ğ”½ n) : (p âˆ© q).toSet = p.toSet âˆ© q.toSet :=
  Quotient.inductionOnâ‚‚ p q LinearSystem.toSet_concat

@[simp] theorem mem_inter {p q : Polyhedron ğ”½ n} {x : Fin n â†’ ğ”½} : x âˆˆ p âˆ© q â†” x âˆˆ p âˆ§ x âˆˆ q := by
  induction p, q using Quotient.indâ‚‚
  simp only [mem_def, toSet_inter, Set.mem_inter_iff]

instance : HasSubset (Polyhedron ğ”½ n) := âŸ¨(Â·.toSet âŠ† Â·.toSet)âŸ©

theorem subset_iff {p q : Polyhedron ğ”½ n} : p âŠ† q â†” âˆ€ x, x âˆˆ p â†’ x âˆˆ q := by
  constructor <;> intro h <;> exact h

theorem empty_subset (p : Polyhedron ğ”½ n) : âˆ… âŠ† p := by simp [subset_iff, not_mem_empty]

theorem subset_univ (p : Polyhedron ğ”½ n) : p âŠ† âŠ¤ := by simp [subset_iff, mem_univ]

instance : SemilatticeInf (Polyhedron ğ”½ n) where
  inf := (Â· âˆ© Â·)
  le := (Â· âŠ† Â·)
  le_refl p := subset_refl p.toSet
  le_trans _ _ _ := Set.Subset.trans
  le_antisymm _ _ h h' := toSet_inj.mp $ subset_antisymm h h'
  inf_le_left _ _ _ h := And.left $ mem_inter.mp h
  inf_le_right _ _ _ h := And.right $ mem_inter.mp h
  le_inf _ _ _ h h' _ hx := mem_inter.mpr $ And.intro (h hx) (h' hx)
