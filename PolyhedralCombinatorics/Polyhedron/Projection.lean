import PolyhedralCombinatorics.Polyhedron.Basic
import PolyhedralCombinatorics.Polyhedron.Notation

import Mathlib.Data.Finset.Sort
import Mathlib.Data.Sum.Order

import Mathlib.Order.WellFoundedSet

import Mathlib.Tactic.LiftLets

import Utils.IsEmpty

variable {ğ”½} [lof : LinearOrderedField ğ”½] {n : â„•}

lemma Finset.mem_filter_univ {Î±} [Fintype Î±] {x : Î±} {p : Î± â†’ Prop} [DecidablePred p]
  : x âˆˆ ({ x | p x } : Finset Î±) â†” p x := by
  simp_rw [Finset.mem_filter, Finset.mem_univ, true_and]

namespace LinearSystem
open Matrix Finset

def projection (P : LinearSystem ğ”½ n) (c : Fin n â†’ ğ”½) := { x | âˆƒ Î± : ğ”½, x + Î± â€¢ c âˆˆ P.solutions }

theorem projection_neg (P : LinearSystem ğ”½ n) (c : Fin n â†’ ğ”½) : P.projection (-c) = P.projection c := by
  simp only [projection, Set.ext_iff, Set.mem_setOf]
  intro x
  constructor
  all_goals (
    intro h
    obtain âŸ¨Î±, hâŸ© := h
    exists -Î±
    simp_all
  )

theorem projection_concat_comm {P Q : LinearSystem ğ”½ n} {c}
  : projection (P ++ Q) c = projection (Q ++ P) c := by
  unfold projection
  simp_rw [concat_solutions, Set.inter_comm]

@[simp low] lemma mem_projection {P : LinearSystem ğ”½ n}
  : x âˆˆ P.projection c â†” âˆƒ Î± : ğ”½, x + Î± â€¢ c âˆˆ P.solutions := Set.mem_setOf

@[simp]
lemma proj_semiSpace_orthogonal {a : Fin n â†’ ğ”½} {b : ğ”½} {c : Fin n â†’ ğ”½} (h : a â¬áµ¥ c = 0)
  : projection (semiSpace a b) c = (semiSpace a b).solutions := by
  simp_rw [projection, Set.ext_iff, Set.mem_setOf, mem_semiSpace]
  intro x
  constructor
  . intro âŸ¨_, h'âŸ©
    simp_rw [dotProduct_add, dotProduct_smul, h, smul_zero, add_zero] at h'
    assumption
  . intro h
    exists 0
    simp [h]

@[simp]
lemma proj_semiSpace_nonorthogonal {a : Fin n â†’ ğ”½} {b : ğ”½} {c : Fin n â†’ ğ”½} (h : a â¬áµ¥ c â‰  0)
  : projection (semiSpace a b) c = Set.univ := by
  simp_rw [projection, mem_semiSpace, Set.eq_univ_iff_forall, Set.mem_setOf]
  intro x
  simp_rw [dotProduct_add, dotProduct_smul, smul_eq_mul]
  exists (b - a â¬áµ¥ x)/(a â¬áµ¥ c)
  simp_rw [div_mul_cancelâ‚€ _ h, add_sub_cancel]
  rfl

@[simp] theorem proj_concat_semiSpace_orthogonal
  {aâ‚ aâ‚‚ : Fin n â†’ ğ”½} {bâ‚ bâ‚‚ : ğ”½} {c : Fin n â†’ ğ”½}
  (hâ‚ : aâ‚ â¬áµ¥ c = 0) (hâ‚‚ : aâ‚‚ â¬áµ¥ c = 0)
  : let Hâ‚ := semiSpace aâ‚ bâ‚
    let Hâ‚‚ := semiSpace aâ‚‚ bâ‚‚
    projection (Hâ‚ ++ Hâ‚‚) c = (Hâ‚ ++ Hâ‚‚).solutions := by
  ext x
  simp_rw [projection, mem_concat, Set.mem_setOf, mem_semiSpace, dotProduct_add, dotProduct_smul,
    hâ‚, hâ‚‚, smul_zero, add_zero, exists_const]

@[simp] theorem proj_concat_semiSpace_orthogonal_left
  {aâ‚ aâ‚‚ : Fin n â†’ ğ”½} {bâ‚ bâ‚‚ : ğ”½} {c : Fin n â†’ ğ”½}
  (hâ‚ : aâ‚ â¬áµ¥ c = 0) (hâ‚‚ : aâ‚‚ â¬áµ¥ c â‰  0)
  : let Hâ‚ := semiSpace aâ‚ bâ‚
    let Hâ‚‚ := semiSpace aâ‚‚ bâ‚‚
    projection (Hâ‚ ++ Hâ‚‚) c = Hâ‚.solutions := by
  ext x
  simp_rw [mem_projection, concat_solutions, Set.mem_inter_iff, mem_semiSpace, dotProduct_add,
    dotProduct_smul, hâ‚, smul_zero, add_zero, exists_and_left, and_iff_left_iff_imp]
  intro
  exists (bâ‚‚ - aâ‚‚ â¬áµ¥ x)/(aâ‚‚ â¬áµ¥ c)
  simp_rw [smul_eq_mul, div_mul_cancelâ‚€ _ hâ‚‚, add_sub_cancel]
  rfl

@[simp] theorem proj_concat_semiSpace_orthogonal_right
  {aâ‚ aâ‚‚ : Fin n â†’ ğ”½} {bâ‚ bâ‚‚ : ğ”½} {c : Fin n â†’ ğ”½}
  (hâ‚ : aâ‚ â¬áµ¥ c â‰  0) (hâ‚‚ : aâ‚‚ â¬áµ¥ c = 0)
  : let Hâ‚ := semiSpace aâ‚ bâ‚
    let Hâ‚‚ := semiSpace aâ‚‚ bâ‚‚
    projection (Hâ‚ ++ Hâ‚‚) c = Hâ‚‚.solutions := by
  extract_lets Hâ‚ Hâ‚‚
  rw [projection_concat_comm]
  exact proj_concat_semiSpace_orthogonal_left (bâ‚ := bâ‚‚) (bâ‚‚ := bâ‚) hâ‚‚ hâ‚

@[simp] theorem proj_concat_semiSpace_nonorthogonal_pos
  {aâ‚ aâ‚‚ : Fin n â†’ ğ”½} {bâ‚ bâ‚‚ : ğ”½} {c : Fin n â†’ ğ”½}
  (hâ‚ : 0 < aâ‚ â¬áµ¥ c) (hâ‚‚ : 0 < aâ‚‚ â¬áµ¥ c)
  : let Hâ‚ := semiSpace aâ‚ bâ‚
    let Hâ‚‚ := semiSpace aâ‚‚ bâ‚‚
    projection (Hâ‚ ++ Hâ‚‚) c = Set.univ := by
  rw [Set.eq_univ_iff_forall]
  intro x
  simp_rw [mem_projection, concat_solutions, Set.mem_inter_iff, mem_semiSpace, dotProduct_add,
    dotProduct_smul, smul_eq_mul]
  let l := (bâ‚ - aâ‚ â¬áµ¥ x)/(aâ‚ â¬áµ¥ c)
  let r := (bâ‚‚ - aâ‚‚ â¬áµ¥ x)/(aâ‚‚ â¬áµ¥ c)
  exists min l r
  constructor
  . calc
      _ â‰¤ aâ‚ â¬áµ¥ x + l * aâ‚ â¬áµ¥ c := by
        rw [add_le_add_iff_left, mul_le_mul_right hâ‚]
        apply min_le_left
      _ = _ := by
        simp_rw [l, div_mul_cancelâ‚€ _ hâ‚.ne.symm, add_sub_cancel]
  . calc
      _ â‰¤ aâ‚‚ â¬áµ¥ x + r * aâ‚‚ â¬áµ¥ c := by
        rw [add_le_add_iff_left, mul_le_mul_right hâ‚‚]
        apply min_le_right
      _ = _ := by
        simp_rw [r, div_mul_cancelâ‚€ _ hâ‚‚.ne.symm, add_sub_cancel]

@[simp] theorem proj_concat_semiSpace_nonorthogonal_neg
  {aâ‚ aâ‚‚ : Fin n â†’ ğ”½} {bâ‚ bâ‚‚ : ğ”½} {c : Fin n â†’ ğ”½}
  (hâ‚ : aâ‚ â¬áµ¥ c < 0) (hâ‚‚ : aâ‚‚ â¬áµ¥ c < 0)
  : let Hâ‚ := semiSpace aâ‚ bâ‚
    let Hâ‚‚ := semiSpace aâ‚‚ bâ‚‚
    projection (Hâ‚ ++ Hâ‚‚) c = Set.univ := by
  extract_lets
  rw [â†projection_neg]
  apply proj_concat_semiSpace_nonorthogonal_pos <;> simp_all

@[simp] theorem proj_concat_semiSpace_nonorthogonal_neg_pos
  {aâ‚ aâ‚‚ : Fin n â†’ ğ”½} {bâ‚ bâ‚‚ : ğ”½} {c : Fin n â†’ ğ”½}
  (hâ‚ : aâ‚ â¬áµ¥ c < 0) (hâ‚‚ : 0 < aâ‚‚ â¬áµ¥ c)
  : let Hâ‚ := semiSpace aâ‚ bâ‚
    let Hâ‚‚ := semiSpace aâ‚‚ bâ‚‚
    let aâ‚ƒ := (aâ‚‚ â¬áµ¥ c) â€¢ aâ‚ - (aâ‚ â¬áµ¥ c) â€¢ aâ‚‚
    let bâ‚ƒ := (aâ‚‚ â¬áµ¥ c) * bâ‚ - (aâ‚ â¬áµ¥ c) * bâ‚‚
    projection (Hâ‚ ++ Hâ‚‚) c = (semiSpace aâ‚ƒ bâ‚ƒ).solutions := by
  ext x
  simp_rw [mem_projection, concat_solutions, Set.mem_inter_iff, mem_semiSpace]
  constructor
  . intro âŸ¨Î±, hâ‚', hâ‚‚'âŸ©
    simp_rw [sub_dotProduct, smul_dotProduct, smul_eq_mul, sub_le_sub_iff, add_comm,
      â†sub_le_sub_iff, â†mul_sub]
    rw [dotProduct_add, dotProduct_smul, â†le_sub_iff_add_le', smul_eq_mul] at hâ‚' hâ‚‚'
    rw [â†div_le_iff_of_neg' hâ‚]
    calc
      _ â‰¤ Î± * aâ‚‚ â¬áµ¥ c := by
        rw [mul_comm, mul_div_assoc, â†div_le_iff_of_neg]
        . calc
          _ â‰¤ Î± * aâ‚ â¬áµ¥ c := by
            rw [div_div_eq_mul_div, mul_assoc, mul_div_assoc, mul_div_cancel_leftâ‚€ _ hâ‚‚.ne.symm]
          _ â‰¤ _ := hâ‚'
        . simp [div_neg_iff, hâ‚, hâ‚‚]
      _ â‰¤ _ := hâ‚‚'
  . intro h
    simp_rw [sub_dotProduct, smul_dotProduct, sub_le_sub_iff, add_comm, â†sub_le_sub_iff,
      smul_eq_mul, â†mul_sub, mul_comm, â†div_le_iffâ‚€ hâ‚‚, mul_div_assoc, â†div_le_iff_of_neg' hâ‚] at h
    exists (bâ‚‚ - aâ‚‚ â¬áµ¥ x)/(aâ‚‚ â¬áµ¥ c)
    simp only [dotProduct_add, dotProduct_smul, smul_eq_mul]
    constructor
    . apply add_le_of_le_sub_left
      rw [â†div_le_iff_of_neg hâ‚]
      assumption
    . simp [div_mul_cancelâ‚€ _ hâ‚‚.ne.symm]

@[simp] theorem proj_concat_semiSpace_nonorthogonal_pos_neg
  {aâ‚ aâ‚‚ : Fin n â†’ ğ”½} {bâ‚ bâ‚‚ : ğ”½} {c : Fin n â†’ ğ”½}
  (hâ‚ : 0 < aâ‚ â¬áµ¥ c) (hâ‚‚ : aâ‚‚ â¬áµ¥ c < 0)
  : let Hâ‚ := semiSpace aâ‚ bâ‚
    let Hâ‚‚ := semiSpace aâ‚‚ bâ‚‚
    let aâ‚ƒ := (aâ‚ â¬áµ¥ c) â€¢ aâ‚‚ - (aâ‚‚ â¬áµ¥ c) â€¢ aâ‚
    let bâ‚ƒ := (aâ‚ â¬áµ¥ c) * bâ‚‚ - (aâ‚‚ â¬áµ¥ c) * bâ‚
    projection (Hâ‚ ++ Hâ‚‚) c = (semiSpace aâ‚ƒ bâ‚ƒ).solutions := by
  extract_lets
  rw [projection_concat_comm]
  apply proj_concat_semiSpace_nonorthogonal_neg_pos <;> assumption

lemma projection_inter_pairs (S : LinearSystem ğ”½ n) (c : Fin n â†’ ğ”½) {x}
  : x âˆˆ S.projection c â†” âˆ€ i j, x âˆˆ (S.ith_semiSpace i ++ S.ith_semiSpace j).projection c := by
  constructor
  . rw [mem_projection]
    intro âŸ¨Î±, hâŸ© i j
    exists Î±
    simp_rw [concat_solutions, Set.mem_inter_iff, mem_semiSpace]
    exact âŸ¨h i, h jâŸ©
  . intro h
    simp_rw [mem_projection] at h âŠ¢
    let N : Finset (Fin S.m) := {i | S.mat i â¬áµ¥ c < 0}
    let Z : Finset (Fin S.m) := {i | S.mat i â¬áµ¥ c = 0}
    let P : Finset (Fin S.m) := {i | S.mat i â¬áµ¥ c > 0}
    let Î› : Fin S.m â†’ ğ”½ := fun i â†¦ (S.vec i - S.mat i â¬áµ¥ x) / (S.mat i â¬áµ¥ c)
    let L := (N.image Î›).max
    let U := (P.image Î›).min
    have âŸ¨Î±, hl, huâŸ© : âˆƒ Î± : ğ”½, L â‰¤ Î± âˆ§ Î± â‰¤ U := by
      match hL : L, hU : U with
      | âŠ¥, âŠ¤ => exact âŸ¨0, bot_le, le_topâŸ©
      | âŠ¥, .some u => exact âŸ¨u, bot_le, le_rflâŸ©
      | .some l, âŠ¤ => exact âŸ¨l, le_rfl, le_topâŸ©
      | .some l, .some u =>
        have hN : N.Nonempty := Finset.image_nonempty.mp âŸ¨l, Finset.mem_of_max hLâŸ©
        have hP : P.Nonempty := Finset.image_nonempty.mp âŸ¨u, Finset.mem_of_min hUâŸ©
        unfold_let L U at hL hU
        let âŸ¨i, hiâ‚, hiâ‚‚âŸ© := Finset.mem_image.mp $ Finset.mem_of_max hL
        let âŸ¨j, hjâ‚, hjâ‚‚âŸ© := Finset.mem_image.mp $ Finset.mem_of_min hU
        simp_rw [N, P, mem_filter_univ] at hiâ‚ hjâ‚
        subst l u
        replace âŸ¨Î±, hâŸ© := h i j
        simp_rw [mem_concat, mem_semiSpace] at h
        simp only [WithBot.coe_le_coe, WithTop.coe_le_coe]
        exists Î±
        constructor <;> by_contra hc <;> rw [not_le] at hc
        . replace h := h.1
          simp_rw [dotProduct_add, dotProduct_smul] at h
          have : S.mat i â¬áµ¥ x + Î± â€¢ (S.mat i â¬áµ¥ c) > S.vec i := calc
            _ > _ + Î› i â€¢ _ := by
              apply add_lt_add_left
              exact mul_lt_mul_of_neg_right hc hiâ‚
            _ = _ := by simp_rw [Î›, smul_eq_mul, div_mul_cancelâ‚€ _ hiâ‚.ne, add_sub_cancel]
          linarith
        . replace h := h.2
          simp_rw [dotProduct_add, dotProduct_smul] at h
          have : S.mat j â¬áµ¥ x + Î± â€¢ (S.mat j â¬áµ¥ c) > S.vec j := calc
            _ > _ + Î› j â€¢ _ := by
              apply add_lt_add_left
              exact mul_lt_mul_of_pos_right hc hjâ‚
            _ = _ := by simp_rw [Î›, smul_eq_mul, div_mul_cancelâ‚€ _ hjâ‚.ne', add_sub_cancel]
          linarith
    exists Î±
    rw [mem_solutions, Pi.le_def, mulVec_add, mulVec_smul]
    intro i
    rw [Pi.add_apply, Pi.smul_apply]
    change S.mat i â¬áµ¥ x + Î± * S.mat i â¬áµ¥ c â‰¤ S.vec i
    rcases lt_trichotomy (S.mat i â¬áµ¥ c) 0 with neg | zero | pos
    . have mem_N : i âˆˆ N := mem_filter_univ.mpr neg
      have : N.Nonempty := âŸ¨_, mem_NâŸ©
      simp only [Finset.max_le_iff, mem_image, WithBot.coe_le_coe, forall_exists_index, and_imp,
        forall_apply_eq_imp_iffâ‚‚, L] at hl
      rw [add_comm, â†le_sub_iff_add_le, â†div_le_iff_of_neg neg]
      exact hl _ mem_N
    . rw [zero, mul_zero, add_zero]
      have : i âˆˆ Z := mem_filter_univ.mpr zero
      have âŸ¨Î±', hÎ±'âŸ© := h i i
      rw [mem_concat, and_self, mem_semiSpace, dotProduct_add, dotProduct_smul, zero, smul_zero,
        add_zero] at hÎ±'
      assumption
    . have mem_P : i âˆˆ P := mem_filter_univ.mpr pos
      have : P.Nonempty := âŸ¨_, mem_PâŸ©
      simp only [Finset.le_min_iff, mem_image, WithTop.coe_le_coe, forall_exists_index, and_imp,
        forall_apply_eq_imp_iffâ‚‚, U] at hu
      rw [add_comm, â†le_sub_iff_add_le, â†le_div_iffâ‚€ pos]
      exact hu _ mem_P

lemma projection_inter_pairs' (S : LinearSystem ğ”½ n) (c : Fin n â†’ ğ”½) {x}
  : x âˆˆ S.projection c â†”
    (âˆ€ i, S.mat i â¬áµ¥ c = 0 â†’ x âˆˆ (S.ith_semiSpace i).solutions)
    âˆ§ (âˆ€ i j, S.mat i â¬áµ¥ c < 0 â†’ S.mat j â¬áµ¥ c > 0 â†’
        x âˆˆ (S.ith_semiSpace i ++ S.ith_semiSpace j).projection c) := by
    apply (S.projection_inter_pairs c).trans
    constructor
    . intro h
      constructor
      . intro i hi
        have := h i i
        rw [proj_concat_semiSpace_orthogonal hi hi, mem_concat, and_self] at this
        exact this
      . intro i j _ _
        apply h
    . intro âŸ¨hz, hnpâŸ© i j
      rcases lt_trichotomy (S.mat i â¬áµ¥ c) 0 with negâ‚ | zeroâ‚ | posâ‚
      <;> rcases lt_trichotomy (S.mat j â¬áµ¥ c) 0 with negâ‚‚ | zeroâ‚‚ | posâ‚‚
      . rw [proj_concat_semiSpace_nonorthogonal_neg]
        repeat trivial
      . rw [proj_concat_semiSpace_orthogonal_right negâ‚.ne zeroâ‚‚]
        apply hz j zeroâ‚‚
      . exact hnp i j negâ‚ posâ‚‚
      . rw [proj_concat_semiSpace_orthogonal_left zeroâ‚ negâ‚‚.ne]
        apply hz i zeroâ‚
      . rw [proj_concat_semiSpace_orthogonal zeroâ‚ zeroâ‚‚, mem_concat]
        exact âŸ¨hz i zeroâ‚, hz j zeroâ‚‚âŸ©
      . rw [proj_concat_semiSpace_orthogonal_left zeroâ‚ posâ‚‚.ne']
        apply hz i zeroâ‚
      . rw [projection_concat_comm]
        exact hnp j i negâ‚‚ posâ‚
      . rw [proj_concat_semiSpace_orthogonal_right posâ‚.ne' zeroâ‚‚]
        apply hz j zeroâ‚‚
      . rw [proj_concat_semiSpace_nonorthogonal_pos]
        repeat trivial

def computeProjection (S : LinearSystem ğ”½ n) (c : Fin n â†’ ğ”½) : LinearSystem ğ”½ n :=
  let N : Finset (Fin S.m) := {i | S.mat i â¬áµ¥ c < 0}
  let Z : Finset (Fin S.m) := {i | S.mat i â¬áµ¥ c = 0}
  let P : Finset (Fin S.m) := {i | S.mat i â¬áµ¥ c > 0}
  let R := Z âŠ•â‚— (N Ã—â‚— P)
  let r := Fintype.card R
  let p : Fin r â‰ƒo R := Fintype.orderIsoFinOfCardEq R rfl
  let D : Matrix (Fin r) (Fin n) ğ”½ := Matrix.of fun i â†¦
    match p i with
    | .inl s => S.mat s
    | .inr (s, t) => (S.mat t â¬áµ¥ c) â€¢ S.mat s - (S.mat s â¬áµ¥ c) â€¢ S.mat t
  let d : Fin r â†’ ğ”½ := fun i â†¦
    match p i with
    | .inl s => S.vec s
    | .inr (s, t) => (S.mat t â¬áµ¥ c) â€¢ S.vec s - (S.mat s â¬áµ¥ c) â€¢ S.vec t
  of D d

@[simp] theorem mem_computeProjection {S : LinearSystem ğ”½ n} {c} {x}
  : x âˆˆ (computeProjection S c).solutions â†” x âˆˆ S.projection c := by
  let A := S.mat
  let b := S.vec
  unfold computeProjection
  lift_lets
  extract_lets _ _ _ _ r p D d
  rw [projection_inter_pairs']
  show (âˆ€ (i : Fin r), D i â¬áµ¥ x â‰¤ d i) â†” _
  constructor
  . intro h
    constructor
    . intro i hi
      rw [mem_semiSpace]
      have := h $ p.symm $ Sum.inl âŸ¨i, mem_filter_univ.mpr hiâŸ©
      simp only [D, d, dotProduct, Matrix.of_apply, OrderIso.apply_symm_apply] at this
      exact this
    . intro i j hi hj
      have := h $ p.symm $ Sum.inr âŸ¨âŸ¨i, mem_filter_univ.mpr hiâŸ©, âŸ¨j, mem_filter_univ.mpr hjâŸ©âŸ©
      simp only [D, d, dotProduct, Matrix.of_apply, OrderIso.apply_symm_apply] at this
      rw [proj_concat_semiSpace_nonorthogonal_neg_pos hi hj, mem_semiSpace]
      exact this
  . intro âŸ¨hz, hnpâŸ© i
    rcases hi : p i with s | âŸ¨s, tâŸ©
    . have hD : D i = A s := by simp only [D, funext_iff, of_apply, hi, implies_true]
      have hd : d i = b s := by simp only [d, hi]
      have hs := Finset.mem_filter_univ.mp s.property
      replace := hz s hs
      rw [mem_semiSpace] at this
      rw [hD, hd]
      exact this
    . have hD : D i = ((A t â¬áµ¥ c) â€¢ A s - (A s â¬áµ¥ c) â€¢ A t) := by simp_all only [D, funext_iff, of_apply, implies_true]
      have hd : d i = (A t â¬áµ¥ c) â€¢ b s - (A s â¬áµ¥ c) â€¢ b t := by simp_all only [d]
      have hs := mem_filter_univ.mp s.property
      have ht := mem_filter_univ.mp t.property
      replace := hnp s t hs ht
      rw [proj_concat_semiSpace_nonorthogonal_neg_pos hs ht, mem_semiSpace] at this
      rw [hD, hd]
      exact this

@[simp] lemma solutions_computeProjection (S : LinearSystem ğ”½ n) (c : Fin n â†’ ğ”½)
  : (computeProjection S c).solutions = S.projection c := by
  simp_rw [Set.ext_iff, mem_projection]
  apply mem_computeProjection

end LinearSystem

namespace Polyhedron
open Matrix LinearSystem

def projection (P : Polyhedron ğ”½ n) (c : Fin n â†’ ğ”½) := {x | âˆƒ Î± : ğ”½, x + Î± â€¢ c âˆˆ P}

@[simp] lemma mem_projection {P : Polyhedron ğ”½ n}
  : x âˆˆ P.projection c â†” âˆƒ Î± : ğ”½, x + Î± â€¢ c âˆˆ P := Set.mem_setOf

def computeProjection (P : Polyhedron ğ”½ n) (c : Fin n â†’ ğ”½) : Polyhedron ğ”½ n :=
  Quotient.liftOn P (fun S â†¦ S.computeProjection c) fun a b h â†¦ by
    apply toSet_inj.mp
    simp_rw [toSet_ofLinearSystem, solutions_computeProjection, LinearSystem.projection]
    rw [h]

@[simp]
theorem mem_computeProjection {P : Polyhedron ğ”½ n} {c : Fin n â†’ ğ”½}
  : x âˆˆ P.computeProjection c â†” âˆƒ Î± : ğ”½, x + Î± â€¢ c âˆˆ P := by
  induction P using Quotient.ind
  unfold computeProjection
  simp_rw [Quotient.lift_mk, mem_ofLinearSystem, solutions_computeProjection]
  rfl

@[simp] theorem subset_computeProjection {P : Polyhedron ğ”½ n} {c : Fin n â†’ ğ”½}
  : P âŠ† P.computeProjection c := by
  intro x
  rw [mem_computeProjection]
  intro x_mem_S
  exists 0
  rw [zero_smul, add_zero]
  assumption

theorem projection_self (P : Polyhedron ğ”½ n) (c) : P âˆ© P.computeProjection c = P := by
  apply subset_antisymm
  . apply inter_subset_left
  . apply subset_inter
    . apply subset_refl
    . apply subset_computeProjection

def fourierMotzkin (P : Polyhedron ğ”½ n) (j : Fin n) :=
  !P{x_[j] = 0} âˆ© P.computeProjection x_[j]

theorem mem_fourierMotzkin (P : Polyhedron ğ”½ n) (j : Fin n) :
  x âˆˆ P.fourierMotzkin j â†” x j = 0 âˆ§ âˆƒ (Î± : ğ”½), x + Pi.single j Î± âˆˆ P := by
  simp_rw [
    fourierMotzkin, mem_inter, mem_computeProjection, mem_ofConstraints,
    List.mem_singleton, forall_eq, LinearConstraint.valid,
    single_dotProduct, one_mul, and_congr_right_iff,
    â†Pi.single_smul, smul_eq_mul, mul_one, implies_true
  ]

@[simp] theorem computeProjection_eq_empty_iff (P : Polyhedron ğ”½ n) (c)
  : P.computeProjection c = âˆ… â†” P = âˆ… := by
  constructor <;> intro h
  . have := projection_self P c
    simp_rw [Polyhedron.ext_iff, mem_inter, mem_computeProjection] at this
    simp_rw [eq_empty_iff, mem_computeProjection] at h âŠ¢
    simp_all
  . simp_rw [eq_empty_iff, mem_computeProjection]
    simp_all [not_mem_empty]

-- def recElimDimensions (p : Polyhedron ğ”½ n) {k : â„•} (h : k â‰¤ n) :=
--   match k with
--   | 0 => p
--   | k + 1 => (p.recElimDimensions $ le_of_add_le_left h).computeProjection x_[âŸ¨k, hâŸ©]

-- @[simp] theorem recElimDimensions_eq_empty_iff (p : Polyhedron ğ”½ n) {k : â„•} (hk : k â‰¤ n)
--   : p.recElimDimensions hk = âˆ… â†” p = âˆ… := by
--   unfold recElimDimensions
--   split
--   . rfl
--   . rw [computeProjection_eq_empty_iff]
--     apply p.recElimDimensions_eq_empty_iff

-- theorem recElimDimensions_all_empty_or_univ (p : Polyhedron ğ”½ n) {k : â„•}
--   : let p' := p.recElimDimensions (le_refl _); p' = âˆ… âˆ¨ p' = âŠ¤ := by
--   unfold recElimDimensions
--   split
--   . by_cases finZeroElim âˆˆ p <;> simp_all [Polyhedron.ext_iff, not_mem_empty, mem_univ]
--   . simp only [Nat.succ_eq_add_one, computeProjection_eq_empty_iff, recElimDimensions_eq_empty_iff]
--     by_cases p = âˆ…
--     . left; assumption
--     . right
--       simp_rw [Polyhedron.ext_iff, mem_computeProjection, mem_univ, iff_true]
--       intro x
--       sorry

end Polyhedron
