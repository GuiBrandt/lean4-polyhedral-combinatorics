import Mathlib.Data.Matrix.Basic
import Mathlib.Analysis.Convex.Basic

/-!
# Linear systems

This file defines basic properties of linear systems.

Linear systems of `n` variables over a linear ordered field `ğ”½` are represented
with `LinearSystem ğ”½ n`.

## Main definitions

* `LinearSystem`: The type of linear systems.
* `LinearSystem.of`: The linear system defined by a `m Ã— n` coefficient matrix
  and an `m Ã— 1` column vector.
* `LinearSystem.concat`: Concatenation of linear systems.
* `LinearSystem.solutions`: Set of solutions of a linear system.

## Main results

* `LinearSystem.solutions_concat`: Characterizes the set of solutions of the
  concatenation of two linear systems.
* `LinearSystem.solutions_convex`: The set of solutions of a linear system is convex.
-/

variable
  (ğ”½ : Type*) -- Field type
  (n : â„•) -- Dimension of the space

/-- `LinearSystem ğ”½ n` is the type of linear systems of inequalities in `ğ”½^n`, where `ğ”½` is a
  linear ordered field. -/
structure LinearSystem where
  /-- Number of inequalities in the system. -/
  m : â„•
  /-- Coefficient matrix of the system. -/
  mat : Matrix (Fin m) (Fin n) ğ”½
  /-- Column-vector of the system's right-hand side. -/
  vec : Fin m â†’ ğ”½

namespace LinearSystem
open Matrix

variable {ğ”½ m n}

/-- Constructs a linear system defined by a `m Ã— n` coefficient matrix `A` and an `m Ã— 1` column
  vector `b`. -/
@[match_pattern] abbrev of (A : Matrix (Fin m) (Fin n) ğ”½) (b : Fin m â†’ ğ”½) : LinearSystem ğ”½ n :=
  âŸ¨m, A, bâŸ©

/-- The empty linear system. -/
@[simp] def empty : LinearSystem ğ”½ n := of vecEmpty vecEmpty

/-- A linear system is empty if and only if it has no rows. -/
theorem eq_empty_iff : p = empty â†” p.m = 0 := by
  constructor <;> intro h
  . rw [h]
    rfl
  . obtain âŸ¨m, A, bâŸ© := p
    simp only at h
    subst m
    suffices A = ![] âˆ§ b = ![] by
      rw [this.1, this.2]
      rfl
    constructor
    . funext x; apply Fin.elim0 x
    . funext x; apply Fin.elim0 x

/-- Prepends a linear inequality `yáµ€ x â‰¤ b` to a linear system `p`. -/
@[simp] def cons (y : Fin n â†’ ğ”½) (b : ğ”½) (p : LinearSystem ğ”½ n) : LinearSystem ğ”½ n :=
  of (vecCons y p.mat) (vecCons b p.vec)

/-- Define `motive p` by induction on `LinearSystem ğ”½ n` via induction on the inequalities of the
  system.

  This function has two arguments: `empty` gives the base case on an empty linear system, and
  `cons` gives the inductive step for a linear system of the form `cons y b p`. -/
@[elab_as_elim] def induction {motive : LinearSystem ğ”½ n â†’ Sort _}
  (empty : motive empty) (cons : âˆ€ y b p, motive p â†’ motive (cons y b p)) : âˆ€ p, motive p
  | âŸ¨m, A, bâŸ© => by
    induction m
    case zero =>
      have : A = vecEmpty := by funext x; exact Fin.elim0 x
      have : b = vecEmpty := by funext x; exact Fin.elim0 x
      subst A b
      exact empty
    case succ m ih =>
      have : âŸ¨_, A, bâŸ© = LinearSystem.cons (vecHead A) (vecHead b) (of (vecTail A) (vecTail b)) := by
        simp only [LinearSystem.cons, Matrix.cons_head_tail]
      simp_rw [this]
      apply cons
      apply ih

/-- A version of `LinearSystem.induction` taking `p : LinearSystem ğ”½ n` as the first argument. -/
@[elab_as_elim] def inductionOn {motive : LinearSystem ğ”½ n â†’ Sort _}
  (p : LinearSystem ğ”½ n) (empty : motive empty) (cons : âˆ€ y b p, motive p â†’ motive (cons y b p))
  : motive p := induction empty cons p

/-- Define `motive p` by separately handling the cases `p = empty` and `p = cons y b q`. --/
@[elab_as_elim] def cases {motive : LinearSystem ğ”½ n â†’ Sort _}
  (empty : motive empty) (cons : âˆ€ y b p, motive (cons y b p))
  : âˆ€ p, motive p := induction empty fun y b p _ => cons y b p

@[simp] theorem cases_empty {motive : LinearSystem ğ”½ n â†’ Sort _}
  {empty : motive empty} {cons : âˆ€ y b p, motive (cons y b p)}
  : cases empty cons LinearSystem.empty = empty := rfl

@[simp] theorem cases_cons {motive : LinearSystem ğ”½ n â†’ Sort _}
  {empty : motive empty} {cons : âˆ€ y b p, motive (cons y b p)}
  : cases empty cons (LinearSystem.cons y b p) = cons y b p := rfl

/-- The concatenation of two linear systems. -/
def concat (p q : LinearSystem ğ”½ n) : LinearSystem ğ”½ n :=
  {
    m := p.m + q.m,
    mat := Fin.append p.mat q.mat
    vec := Fin.append p.vec q.vec
  }

instance : Append (LinearSystem ğ”½ n) := âŸ¨concatâŸ©

theorem concat_m {p q : LinearSystem ğ”½ n} : (p ++ q).m = p.m + q.m := rfl
theorem concat_mat {p q : LinearSystem ğ”½ n} : (p ++ q).mat = Fin.append p.mat q.mat := rfl
theorem concat_vec {p q : LinearSystem ğ”½ n} : (p ++ q).vec = Fin.append p.vec q.vec := rfl

variable [LinearOrderedField ğ”½] (p : LinearSystem ğ”½ n)

/-- The set of solutions of the linear system. -/
@[simp low] def solutions : Set (Fin n â†’ ğ”½) := { x | p.mat *áµ¥ x â‰¤ p.vec }

/-- Two linear systems are said to be equivalent when their sets of solutions are equal. -/
instance : HasEquiv (LinearSystem ğ”½ n) := âŸ¨(Â·.solutions = Â·.solutions)âŸ©

instance isSetoid (ğ”½ n) [LinearOrderedField ğ”½] : Setoid (LinearSystem ğ”½ n) :=
  âŸ¨instHasEquiv.Equiv, fun _ â†¦ rfl, Eq.symm, Eq.transâŸ©

@[simp] theorem equiv_def : p â‰ˆ q â†” p.solutions = q.solutions := Iff.rfl

@[simp] lemma mem_solutions : x âˆˆ p.solutions â†” p.mat *áµ¥ x â‰¤ p.vec := Set.mem_setOf

@[simp] lemma mem_solutions_of {x : Fin n â†’ ğ”½} : x âˆˆ (of A b).solutions â†” A *áµ¥ x â‰¤ b := Set.mem_setOf

@[simp] theorem empty_solutions : (empty : LinearSystem ğ”½ n).solutions = Set.univ := by simp

@[simp] lemma vecCons_mulVec
  {m n : â„•} (y : Fin n â†’ ğ”½) (A : Matrix (Fin m) (Fin n) ğ”½) (x : Fin n â†’ ğ”½)
  : vecCons y A *áµ¥ x = vecCons (y â¬áµ¥ x) (A *áµ¥ x) := by
  funext x
  cases x using Fin.cases <;> rfl

@[simp] lemma vecCons_le_vecCons {n : â„•} (a b : ğ”½) (x y : Fin n â†’ ğ”½)
  : vecCons a x â‰¤ vecCons b y â†” a â‰¤ b âˆ§ x â‰¤ y := by
  simp_rw [Pi.le_def]
  constructor <;> intro h
  . constructor
    . exact h 0
    . intro i
      exact h i.succ
  . intro i
    cases i using Fin.cases
    . simp only [cons_val_zero, h.1]
    . simp only [cons_val_succ, h.2]

@[simp] theorem cons_solutions
  : (cons y b p : LinearSystem ğ”½ n).solutions = {x | y â¬áµ¥ x â‰¤ b âˆ§ p.mat *áµ¥ x â‰¤ p.vec} := by simp

/-- The set of solutions of the concatenation of two linear systems is the intersection of their
  sets of solutions. -/
@[simp low] theorem concat_solutions : (p ++ q).solutions = p.solutions âˆ© q.solutions := by
  simp_rw [Set.ext_iff, Set.mem_inter_iff]
  intro x
  constructor <;> intro h
  . simp_rw [mem_solutions, concat_mat, concat_vec, Pi.le_def] at h
    constructor <;> (rw [mem_solutions, Pi.le_def]; intro i)
    . have := h (i.castAdd q.m)
      simp_all [mulVec]
    . have := h (i.natAdd p.m)
      simp_all [mulVec]
  . simp_rw [mem_solutions, concat_m, concat_mat, concat_vec, Pi.le_def]
    intro i
    cases i using Fin.addCases
    <;> simp only [mulVec, Fin.append_left, Fin.append_right]
    . apply h.1
    . apply h.2

@[simp] theorem mem_concat : x âˆˆ (p ++ q).solutions â†” x âˆˆ p.solutions âˆ§ x âˆˆ q.solutions := by
  rw [â†Set.mem_inter_iff, concat_solutions]

/-- The set of solutions of a linear system is convex. -/
theorem solutions_convex : Convex ğ”½ p.solutions := by
  intro x x_mem_p y y_mem_p Î± Î² Î±_nonneg Î²_nonneg Î±Î²_affine
  calc
    p.mat *áµ¥ (Î± â€¢ x + Î² â€¢ y) = Î± â€¢ p.mat *áµ¥ x + Î² â€¢ p.mat *áµ¥ y := by
      simp_rw [mulVec_add, mulVec_smul]
    _ â‰¤ Î± â€¢ p.vec + Î² â€¢ p.vec :=
      add_le_add
        (smul_le_smul_of_nonneg_left x_mem_p Î±_nonneg)
        (smul_le_smul_of_nonneg_left y_mem_p Î²_nonneg)
    _ = p.vec := by rw [â†add_smul, Î±Î²_affine, one_smul]

/-- A system defined by a single equation `aáµ€ x â‰¤ b`. -/
def semiSpace (a : Fin n â†’ ğ”½) (b : ğ”½) : LinearSystem ğ”½ n := of ![a] ![b]

section
omit [LinearOrderedField ğ”½]

theorem semiSpace_m {a : Fin n â†’ ğ”½} {b : ğ”½} : (semiSpace a b).m = 1 :=
  rfl
theorem semiSpace_mat {a : Fin n â†’ ğ”½} {b : ğ”½} : (semiSpace a b).mat = ![a] := rfl
theorem semiSpace_vec {a : Fin n â†’ ğ”½} {b : ğ”½} : (semiSpace a b).vec = ![b] := rfl

end

@[simp low] theorem semiSpace_solutions : (semiSpace a b).solutions = { x : Fin n â†’ ğ”½ | a â¬áµ¥ x â‰¤ b } := by
  simp [semiSpace]

@[simp] theorem mem_semiSpace : âˆ€ x : Fin n â†’ ğ”½, x âˆˆ (semiSpace a b).solutions â†” a â¬áµ¥ x â‰¤ b := by
  simp [semiSpace]

abbrev ith_semiSpace (S : LinearSystem ğ”½ n) (i : Fin S.m) : LinearSystem ğ”½ n :=
  semiSpace (S.mat i) (S.vec i)
