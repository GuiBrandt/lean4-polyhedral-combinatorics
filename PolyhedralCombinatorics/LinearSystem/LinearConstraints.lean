import PolyhedralCombinatorics.LinearSystem.Basic

/-!
# Linear constraints in linear systems

Although inequalities of the form `yáµ€ x â‰¤ b` (where `y` is a coefficient vector
and `b` is a scalar) are sufficient to represent all linear systems, it is
often convenient to be able to use equalities (`yáµ€ x = b`) or inequalities in
the other direction (`yáµ€ x â‰¥ b`).

To this end, we hereby define the type of linear constraints and prove some
relevant theorems about how linear systems defined by a set of constraints
behave.

## Main definitions

* `LinearConstraint`: The type of linear constraints.
* `LinearSystem.ofConstraints`: The linear system corresponding to a list of
  constraints.

## Main results

* `LinearSystem.solutions_ofConstraints`: Characterization of the solutions of
  a linear system in terms of the constraints that define it.
-/

variable
  (ğ”½ : Type*) [LinearOrderedField ğ”½] -- Field type
  (n : â„•) -- Dimension of the space

/-- `LinearConstraint n` is the type of linear constraints on `n` variables. -/
inductive LinearConstraint (ğ”½ : Type*) (n : â„•) where
| le : (Fin n â†’ ğ”½) â†’ ğ”½ â†’ LinearConstraint ğ”½ n
| eq : (Fin n â†’ ğ”½) â†’ ğ”½ â†’ LinearConstraint ğ”½ n
| ge : (Fin n â†’ ğ”½) â†’ ğ”½ â†’ LinearConstraint ğ”½ n

variable {ğ”½ n} {y : Fin n â†’ ğ”½} {b : ğ”½}

namespace LinearConstraint
open Matrix

/-- Whether a linear constraint is valid for a given vector. -/
@[simp] def valid : LinearConstraint ğ”½ n â†’ (Fin n â†’ ğ”½) â†’ Prop
| le y b, x => y â¬áµ¥ x â‰¤ b
| eq y b, x => y â¬áµ¥ x = b
| ge y b, x => y â¬áµ¥ x â‰¥ b

lemma eq_valid_iff : (eq y b).valid x â†” (le y b).valid x âˆ§ (ge y b).valid x := by
  simp [le_antisymm_iff]

end LinearConstraint

namespace LinearSystem

open LinearConstraint Matrix

/-- Constructs a linear system from a list of linear constraints. -/
def ofConstraints : List (LinearConstraint ğ”½ n) â†’ LinearSystem ğ”½ n
  | [] => of vecEmpty vecEmpty
  | c :: cs =>
    match c with
    | le y b => cons y b $ ofConstraints cs
    | ge y b => cons (-y) (-b) $ ofConstraints cs
    | eq y b => cons y b $ cons (-y) (-b) $ ofConstraints cs

section toSet_ofConstraints
open Matrix LinearConstraint

variable {cs : List (LinearConstraint ğ”½ n)} {y : Fin n â†’ ğ”½} {b : ğ”½}

@[simp] lemma ofConstraints_nil
  : (@ofConstraints ğ”½ _ n  []) = of vecEmpty vecEmpty := rfl

theorem mem_ofConstraints_nil_solutions : x âˆˆ (@ofConstraints ğ”½ _ n []).solutions := by simp

@[simp] private lemma vecCons_mulVec
  {m n : â„•} (y : Fin n â†’ ğ”½) (A : Matrix (Fin m) (Fin n) ğ”½) (x : Fin n â†’ ğ”½)
  : vecCons y A *áµ¥ x = vecCons (y â¬áµ¥ x) (A *áµ¥ x) := by
  funext x
  cases x using Fin.cases <;> rfl

@[simp] private lemma vecCons_le_vecCons {n : â„•} (a b : ğ”½) (x y : Fin n â†’ ğ”½)
  : vecCons a x â‰¤ vecCons b y â†” a â‰¤ b âˆ§ x â‰¤ y := by
  simp_rw [Pi.le_def]
  constructor <;> intro h
  . constructor
    . exact h 0
    . intro i
      exact h i.succ
  . intro i
    cases i using Fin.cases
    . simp only [cons_val_zero, h.1]
    . simp only [cons_val_succ, h.2]

@[simp] lemma mem_ofConstraints_cons_solutions
  : x âˆˆ (@ofConstraints ğ”½ _ n $ c :: cs).solutions â†” c.valid x âˆ§ x âˆˆ (ofConstraints cs).solutions := by
  simp only [solutions, Set.mem_setOf_eq, valid, ge_iff_le]
  rw [ofConstraints]
  split <;> simp [le_antisymm_iff, and_assoc]

@[simp] lemma ofConstraints_le_cons
  : (ofConstraints $ le y b :: cs) = cons y b (ofConstraints cs) := by
  rfl

@[simp] lemma ofConstraints_ge_cons
  : (ofConstraints $ ge y b :: cs) = cons (-y) (-b) (ofConstraints cs) := by
  rfl

@[simp] lemma ofConstraints_eq_cons
  : (ofConstraints $ eq y b :: cs) =
    cons y b (cons (-y) (-b) $ ofConstraints cs) := by
  rfl

@[simp] theorem mem_solutions_ofConstraints (x : Fin n â†’ ğ”½)
  : x âˆˆ (ofConstraints cs).solutions â†” âˆ€ c âˆˆ cs, c.valid x := by
  induction cs
  case nil =>
    simp
  case cons c cs ih =>
    simp only [mem_ofConstraints_cons_solutions, List.forall_mem_cons, ih]

/-- The set of solutions of a linear system of constraints is the set of all points that satisfy
  all constraints. -/
@[simp] theorem solutions_ofConstraints
  : (ofConstraints cs).solutions = { x : Fin n â†’ ğ”½ | âˆ€ c âˆˆ cs, c.valid x } :=
  Set.ext_iff.mpr mem_solutions_ofConstraints

end toSet_ofConstraints

end LinearSystem
