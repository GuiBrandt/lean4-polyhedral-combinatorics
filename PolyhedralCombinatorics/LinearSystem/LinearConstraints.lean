import PolyhedralCombinatorics.LinearSystem.Basic

/-!
# Linear constraints in linear systems

Although inequalities of the form `y·µÄ x ‚â§ b` (where `y` is a coefficient vector
and `b` is a scalar) are sufficient to represent all linear systems, it is
often convenient to be able to use equalities (`y·µÄ x = b`) or inequalities in
the other direction (`y·µÄ x ‚â• b`).

To this end, we hereby define the type of linear constraints and prove some
relevant theorems about how linear systems defined by a set of constraints
behave.

## Main definitions

* `LinearConstraint`: The type of linear constraints.
* `LinearSystem.ofConstraints`: The linear system corresponding to a list of
  constraints.

## Main results

* `LinearSystem.solutions_ofConstraints`: Characterization of the solutions of
  a linear system in terms of the constraints that define it.
-/

variable
  (ùîΩ : Type*) [LinearOrderedField ùîΩ] -- Field type
  (n : ‚Ñï) -- Dimension of the space

inductive LinearConstraint.Comparator | le | eq | ge

/-- `LinearConstraint n` is the type of linear constraints on `n` variables. -/
structure LinearConstraint where
  coefficients : Fin n ‚Üí ùîΩ
  comparator : LinearConstraint.Comparator
  rhs : ùîΩ

variable {ùîΩ n} {y : Fin n ‚Üí ùîΩ} {b : ùîΩ}

namespace LinearConstraint
open Matrix

@[match_pattern] abbrev le (y : Fin n ‚Üí ùîΩ) (b : ùîΩ) : LinearConstraint ùîΩ n := ‚ü®y, .le, b‚ü©
@[match_pattern] abbrev eq (y : Fin n ‚Üí ùîΩ) (b : ùîΩ) : LinearConstraint ùîΩ n := ‚ü®y, .eq, b‚ü©
@[match_pattern] abbrev ge (y : Fin n ‚Üí ùîΩ) (b : ùîΩ) : LinearConstraint ùîΩ n := ‚ü®y, .ge, b‚ü©

/-- Converts a linear constraint into a list o extended matrix rows for a
  linear system. -/
def toExtendedRows : LinearConstraint ùîΩ n ‚Üí List ((Fin n ‚Üí ùîΩ) √ó ùîΩ)
| le y b => [(y, b)]
| eq y b => [(y, b), (-y, -b)]
| ge y b => [(-y, -b)]

@[simp] lemma le_toExtendedRows : toExtendedRows (le y b) = [(y, b)] := rfl
@[simp] lemma eq_toExtendedRows : toExtendedRows (eq y b) = [(y, b), (-y, -b)] := rfl
@[simp] lemma ge_toExtendedRows : toExtendedRows (ge y b) = [(-y, -b)] := rfl

def Comparator.valid : Comparator ‚Üí ùîΩ ‚Üí ùîΩ ‚Üí Prop
| le => (¬∑ ‚â§ ¬∑) | eq => (¬∑ = ¬∑) | ge => (¬∑ ‚â• ¬∑)

/-- Whether a linear constraint is valid for a given vector. -/
def valid : LinearConstraint ùîΩ n ‚Üí (Fin n ‚Üí ùîΩ) ‚Üí Prop
| ‚ü®y, cmp, b‚ü©, x => cmp.valid (y ‚¨ù·µ• x) b

@[simp] lemma le_valid : (le y b).valid x = (y ‚¨ù·µ• x ‚â§ b) := rfl
@[simp] lemma eq_valid : (eq y b).valid x = (y ‚¨ù·µ• x = b) := rfl
@[simp] lemma ge_valid : (ge y b).valid x = (y ‚¨ù·µ• x ‚â• b) := rfl

lemma eq_valid_iff : (eq y b).valid x ‚Üî (le y b).valid x ‚àß (ge y b).valid x := by
  simp [le_antisymm_iff]

end LinearConstraint

namespace LinearSystem

open LinearConstraint

/-- Constructs a linear system from a list of linear constraints. -/
def ofConstraints (cs : List (LinearConstraint ùîΩ n)) : LinearSystem ùîΩ n :=
  let rows := cs.bind toExtendedRows
  ‚ü®rows.length, Matrix.of (Prod.fst ‚àò rows.get), Prod.snd ‚àò rows.get‚ü©

section toSet_ofConstraints
open Matrix LinearConstraint

variable {cs : List (LinearConstraint ùîΩ n)} {y : Fin n ‚Üí ùîΩ} {b : ùîΩ}

@[simp]
lemma ofConstraints_mat_apply : (ofConstraints cs).mat i = (cs.bind toExtendedRows)[i].1 := rfl

@[simp]
lemma ofConstraints_vec_apply : (ofConstraints cs).vec i = (cs.bind toExtendedRows)[i].2 := rfl

@[simp] lemma ofConstraints_apply_eq_lemma
  : let s := ofConstraints cs
    s.mat i = y ‚àß s.vec i = b ‚Üî (cs.bind toExtendedRows)[i] = (y, b) := by
  simp only [ofConstraints_mat_apply, ofConstraints_vec_apply]
  apply Prod.mk.inj_iff.symm

macro "linear_constraint_lemma_tac" h:ident : tactic =>
  `(tactic|
    simp_rw [ofConstraints_apply_eq_lemma]
    <;> exact List.mem_iff_get.mp $ List.mem_bind.mpr ‚ü®_, $h, by simp [toExtendedRows]‚ü©)

private lemma le_lemma (h : ‚ü®y, Comparator.le, b‚ü© ‚àà cs)
  : let s := ofConstraints cs; ‚àÉ i, s.mat i = y ‚àß s.vec i = b := by
  linear_constraint_lemma_tac h

private lemma ge_lemma (h : ‚ü®y, Comparator.ge, b‚ü© ‚àà cs)
  : let s := ofConstraints cs; ‚àÉ i, -(s.mat i) = y ‚àß -(s.vec i) = b := by
  simp_rw [neg_eq_iff_eq_neg]
  linear_constraint_lemma_tac h

private lemma eq_lemma (h : ‚ü®y, Comparator.eq, b‚ü© ‚àà cs)
  : let s := ofConstraints cs;
    (‚àÉ i, s.mat i = y ‚àß s.vec i = b) ‚àß (‚àÉ i, -(s.mat i) = y ‚àß -(s.vec i) = b) := by
  simp_rw [neg_eq_iff_eq_neg]
  constructor <;> linear_constraint_lemma_tac h

@[simp] theorem mem_solutions_ofConstraints (x : Fin n ‚Üí ùîΩ)
  : x ‚àà (ofConstraints cs).solutions ‚Üî ‚àÄ c ‚àà cs, c.valid x := by
  let rows := cs.bind toExtendedRows
  constructor
  . show x ‚àà (ofConstraints cs).solutions ‚Üí ‚àÄ c ‚àà cs, c.valid _
    intro h ‚ü®y, cmp, b‚ü© mem_cs
    cases cmp
    case' le => have ‚ü®_, hy, hb‚ü© := le_lemma mem_cs
    case' ge => have ‚ü®_, hy, hb‚ü© := ge_lemma mem_cs
    case' eq =>
      rw [eq_valid_iff]
      constructor
      case' left => have ‚ü®_, hy, hb‚ü© := (eq_lemma mem_cs).1
      case' right => have ‚ü®_, hy, hb‚ü© := (eq_lemma mem_cs).2
    all_goals subst hy hb
    case le | eq.left =>
      apply h
    case ge | eq.right =>
      simp_rw [ge_valid, neg_dotProduct, neg_le_neg_iff]
      apply h
  . show (‚àÄ c ‚àà cs, c.valid x) ‚Üí ‚àÄ (i : Fin rows.length), rows[i].1 ‚¨ù·µ• _ ‚â§ rows[i].2
    intro h i
    have : rows[i] ‚àà rows := List.get_mem rows i i.prop
    have ‚ü®‚ü®y, cmp, b‚ü©, mem_cs, h'‚ü© := List.mem_bind.mp this
    have c_valid := h _ mem_cs
    cases cmp
    case' eq =>
      rw [eq_valid_iff] at c_valid
      rw [toExtendedRows, List.mem_pair] at h'
      cases h'
    case le | eq.inl =>
      rw [le_valid] at c_valid
      simp_all only [toExtendedRows, List.mem_singleton]
    case ge | eq.inr =>
      rw [ge_valid] at c_valid
      simp_all only [toExtendedRows, List.mem_singleton, neg_dotProduct, neg_le_neg_iff]

/-- The set of solutions of a linear system of constraints is the set of all points that satisfy
  all constraints. -/
@[simp] theorem solutions_ofConstraints
  : (ofConstraints cs).solutions = { x : Fin n ‚Üí ùîΩ | ‚àÄ c ‚àà cs, c.valid x } :=
  Set.ext_iff.mpr mem_solutions_ofConstraints

end toSet_ofConstraints

end LinearSystem
